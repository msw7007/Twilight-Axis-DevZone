/datum/sex_action_session
	var/action_type
	var/datum/sex_panel_action/action
	var/datum/sex_session_tgui/session

	var/mob/living/carbon/human/actor
	var/mob/living/carbon/human/partner

	var/instance_id
	var/actor_node_id
	var/partner_node_id

	var/speed = SEX_SPEED_MID
	var/force = SEX_FORCE_MID

	var/next_tick_time = 0

/datum/sex_action_session/New(datum/sex_session_tgui/S, datum/sex_panel_action/A, actor_node, partner_node)
	. = ..()
	session = S

	START_PROCESSING(SSerp_sytem, src)

	if(A)
		var/datum/sex_panel_action/new_action = new A.type

		new_action.name                   = A.name
		new_action.stamina_cost           = A.stamina_cost
		new_action.affects_self_arousal   = A.affects_self_arousal
		new_action.affects_arousal        = A.affects_arousal
		new_action.affects_self_pain      = A.affects_self_pain
		new_action.affects_pain           = A.affects_pain

		new_action.required_init          = A.required_init
		new_action.required_target        = A.required_target
		new_action.armor_slot_init        = A.armor_slot_init
		new_action.armor_slot_target      = A.armor_slot_target
		new_action.can_knot               = A.can_knot
		new_action.reserve_target_for_session = A.reserve_target_for_session
		new_action.climax_liquid_mode_active  = A.climax_liquid_mode_active
		new_action.climax_liquid_mode_passive = A.climax_liquid_mode_passive

		new_action.message_on_start        = A.message_on_start
		new_action.message_on_perform      = A.message_on_perform
		new_action.message_on_finish       = A.message_on_finish
		new_action.message_on_climax_actor = A.message_on_climax_actor
		new_action.message_on_climax_target = A.message_on_climax_target

		new_action.actor_sex_hearts            = A.actor_sex_hearts
		new_action.target_sex_hearts           = A.target_sex_hearts
		new_action.actor_suck_sound            = A.actor_suck_sound
		new_action.target_suck_sound           = A.target_suck_sound
		new_action.actor_make_sound            = A.actor_make_sound
		new_action.target_make_sound           = A.target_make_sound
		new_action.actor_make_fingering_sound  = A.actor_make_fingering_sound
		new_action.target_make_fingering_sound = A.target_make_fingering_sound
		new_action.actor_do_onomatopoeia       = A.actor_do_onomatopoeia
		new_action.target_do_onomatopoeia      = A.target_do_onomatopoeia
		new_action.actor_do_thrust             = A.actor_do_thrust
		new_action.target_do_thrust            = A.target_do_thrust

		new_action.session = src

		action = new_action
		action_type = action.type

	instance_id = "[REF(src)]"
	actor_node_id = actor_node
	partner_node_id = partner_node

	actor = S.user
	var/mob/living/carbon/human/p = S.get_current_partner()
	if(!p)
		p = S.user
	partner = p

/datum/sex_action_session/Destroy()
	STOP_PROCESSING(SSerp_sytem, src)

	var/datum/sex_organ/src_org = session?.resolve_organ_datum(actor, actor_node_id)
	if(src_org)
		src_org.unbind()

	qdel(action)
	action = null

	return ..()

/datum/sex_action_session/proc/start()
	var/datum/sex_organ/src_org = session.resolve_organ_datum(actor, actor_node_id)
	var/datum/sex_organ/tgt_org = session.resolve_organ_datum(partner, partner_node_id)

	if(src_org)
		src_org.bind_with(tgt_org)

	action.on_start(actor, partner, src)
	next_tick_time = world.time

/datum/sex_action_session/process(delta_time)
	if(next_tick_time && world.time < next_tick_time)
		return

	if(QDELETED(session))
		return

	if(QDELETED(actor) || QDELETED(partner))
		session.stop_instance(instance_id)
		return

	var/datum/sex_action_session/I = session.current_actions?[instance_id]
	if(!I || I != src)
		return

	if(isnull(partner))
		session.stop_instance(instance_id)
		return

	if(!session.can_continue_action_session(src))
		session.stop_instance(instance_id)
		return

	var/do_time = action.interaction_timer / get_speed_multiplier(speed)
	if(do_time < world.tick_lag)
		do_time = world.tick_lag

	if(action.stamina_cost)
		var/mob/living/carbon/human/U = actor
		if(U)
			U.sex_procs_active = TRUE
			var/success = U.stamina_add(action.stamina_cost * get_stamina_cost_multiplier(force))
			U.sex_procs_active = FALSE
			if(!success)
				session.stop_instance(instance_id)
				return

	var/mob/living/carbon/human/A = actor
	var/mob/living/carbon/human/T = partner

	var/datum/sex_organ/src_org = session.resolve_organ_datum(A, actor_node_id)
	var/datum/sex_organ/tgt_org = session.resolve_organ_datum(T, partner_node_id)

	var/self_pleasure_base   = action.affects_self_arousal
	var/target_pleasure_base = action.affects_arousal

	var/self_pain_base   = action.affects_self_pain
	var/target_pain_base = action.affects_pain

	var/list/force_mults = get_force_multipliers(force, A)
	var/pain_mult     = force_mults["pain"]
	var/pleasure_mult = force_mults["pleasure"]
			
	var/self_pleasure_delta   = self_pleasure_base * pleasure_mult
	var/target_pleasure_delta = target_pleasure_base * pleasure_mult

	var/total_pain_mult = pain_mult * ORG_PAIN_GAIN_RATE

	var/self_pain_delta   = self_pain_base   * total_pain_mult
	var/target_pain_delta = target_pain_base * total_pain_mult

	if(A && T)
		var/datum/component/knotting/K = A.GetComponent(/datum/component/knotting)
		if(K && K.knotted_status == KNOTTED_AS_TOP && K.knotted_recipient == T)
			target_pain_delta += 0.25

	if(src_org && self_pain_delta > 0)
		src_org.adjust_pain(self_pain_delta)
	if(tgt_org && target_pain_delta > 0)
		tgt_org.adjust_pain(target_pain_delta)

	apply_arousal_delta(self_pleasure_delta, target_pleasure_delta, self_pain_delta, target_pain_delta)

	session.sync_arousal_ui()
	SStgui.update_uis(session)
	next_tick_time = world.time + do_time

/datum/sex_action_session/proc/apply_arousal_delta(self_delta, partner_delta, self_pain_delta, partner_pain_delta)
	if(self_delta <= 0 && partner_delta <= 0 && self_pain_delta <= 0 && partner_pain_delta <= 0)
		return

	var/mob/living/carbon/human/U = actor
	var/mob/living/carbon/human/T = partner

	var/user_sources   = get_arousal_source_count_for(U)
	var/target_sources = get_arousal_source_count_for(T)

	var/user_delta   = self_delta
	var/target_delta = partner_delta

	if(user_sources > 1 && user_delta)
		user_delta /= user_sources
	if(target_sources > 1 && target_delta)
		target_delta /= target_sources

	if(self_pain_delta > 0 && U)
		if(is_maso(U) || is_nympho(U))
			user_delta += self_pain_delta
		else
			user_delta -= self_pain_delta

	if(partner_pain_delta > 0 && T)
		if(is_maso(T) || is_nympho(T))
			target_delta += partner_pain_delta
		else
			target_delta -= partner_pain_delta

	var/total_user_pain   = 0
	var/total_target_pain = 0

	if(U && actor_node_id && session)
		var/datum/sex_organ/user_org = session.resolve_organ_datum(U, actor_node_id)
		if(user_org)
			total_user_pain = max(0, user_org.pain)
			user_delta *= user_org.sensivity
			user_org.pain += self_pain_delta

	if(T && partner_node_id && session)
		var/datum/sex_organ/target_org = session.resolve_organ_datum(T, partner_node_id)
		if(target_org)
			total_target_pain = max(0, target_org.pain)
			target_delta *= target_org.sensivity
			target_org.pain += partner_pain_delta

	var/can_moan_user = TRUE
	if(U && (user_delta || total_user_pain))
		if(session && !session.allow_user_moan && U == session.user)
			can_moan_user = FALSE

	if(U && (user_delta || total_user_pain))
		SEND_SIGNAL(U, COMSIG_SEX_RECEIVE_ACTION, user_delta, total_user_pain, TRUE, force, speed, actor_node_id, can_moan_user)

	if(T && (target_delta || total_target_pain))
		SEND_SIGNAL(T, COMSIG_SEX_RECEIVE_ACTION, target_delta, total_target_pain, FALSE, force, speed, partner_node_id)

/datum/sex_action_session/proc/get_arousal_source_count_for(mob/living/carbon/human/M)
	if(!M || !session || !length(session.current_actions))
		return 1

	var/count = 0
	for(var/id in session.current_actions)
		var/datum/sex_action_session/S = session.current_actions[id]
		if(!S || QDELETED(S))
			continue
		if(!S.action)
			continue

		if(S.actor == M)
			if(!S.action.affects_self_arousal)
				continue
		else if(S.partner == M)
			if(!S.action.affects_arousal)
				continue
		else
			continue

		count++

	if(count <= 0)
		count = 1
	return count

/datum/sex_action_session/proc/get_priority_for(mob/living/carbon/human/U)
	if(!session || !action || !U)
		return -1

	var/organ_priority = 0
	if(actor_node_id)
		var/org_type = session.node_organ_type(actor_node_id)
		if(isnum(org_type))
			organ_priority = org_type

	var/role_priority = 0
	if(U == partner)
		role_priority = 100
	else if(U == actor)
		role_priority = 50

	return organ_priority + role_priority

/datum/sex_action_session/proc/get_force_multipliers(force, mob/living/carbon/human/unit)
	var/pain_mult = 0.0
	var/pleasure_mult = 1.0

	switch(force)
		if(SEX_FORCE_LOW)
			pain_mult = 0.0
			pleasure_mult = 0.5

		if(SEX_FORCE_MID)
			pain_mult = 0.0
			pleasure_mult = 1.0

		if(SEX_FORCE_HIGH)
			pain_mult = 1.0
			pleasure_mult = is_maso(unit) ? 1.5 : 1.25
			pleasure_mult += is_nympho(unit) ? 0.05 : 0

		if(SEX_FORCE_EXTREME)
			pain_mult = is_maso(unit) ? 1.25 : 1.5
			pleasure_mult = is_maso(unit) ? 2.0 : 1.5

	pleasure_mult += is_nympho(unit) ? 0.25 : 0

	return list(
		"pain"     = pain_mult,
		"pleasure" = pleasure_mult,
	)
